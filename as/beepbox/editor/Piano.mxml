<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (C) 2012 John Nesky

Permission is hereby granted, free of charge, to any person obtaining a copy of 
this software and associated documentation files (the "Software"), to deal in 
the Software without restriction, including without limitation the rights to 
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies 
of the Software, and to permit persons to whom the Software is furnished to do 
so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all 
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR 
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, 
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE 
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER 
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, 
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE 
SOFTWARE.
-->

<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
           xmlns:s="library://ns.adobe.com/flex/spark"
           xmlns="*"
           width="32"
           height="481"
           backgroundColor="0x000000"
           creationComplete="init()"
           addedToStage="onAddedToStage()">
	<mx:Script><![CDATA[
		import flash.display.*;
		import flash.events.*;
		import flash.geom.*;
		import flash.media.*;
		import flash.text.*;
		import flash.ui.*;
		import flash.utils.*;
		
		import mx.collections.ArrayCollection;
		
		import beepbox.synth.*;
		
		public var pitchHeight: Number;
		public var pitchCount: int;
		
		[Bindable]
		public var doc: Document;
		
		[Embed(source="assets/BlackKey.png")]
		private static const BlackKey: Class;
		[Embed(source="assets/BlackKeyDisabled.png")]
		private static const BlackKeyDisabled: Class;
		[Embed(source="assets/WhiteKey.png")]
		private static const WhiteKey: Class;
		[Embed(source="assets/WhiteKeyDisabled.png")]
		private static const WhiteKeyDisabled: Class;
		[Embed(source="assets/Drum.png")]
		private static const Drum: Class;
		
		private var preview: Sprite;
		private var mouseDown: Boolean = false;
		private var mouseOver: Boolean = false;
		private var labels: Array;
		private var keys: Array;
		private var cursorPitch: int;
		
		public function init():void {
			keys = [];
			labels = [];
			for (var i: int = 0; i < Music.pitchCount; i++) {
				var format: TextFormat = new TextFormat();
				format.align = TextFormatAlign.CENTER;
				format.bold = true;
				format.font = "Helvetica";
				format.size = 11;
				var text: TextField = new TextField();
				text.width = 20;
				text.x = 13;
				text.height = 20;
				text.y = 13 * (Music.pitchCount - i - 1);
				text.wordWrap = true;
				text.type = TextFieldType.DYNAMIC;
				text.selectable = false;
				text.mouseEnabled = false;
				text.defaultTextFormat = format;
				container.addChild(text);
				labels.push(text);
			}
			preview = new Sprite();
			container.addChild(preview);
			doc.watch(documentChanged);
			documentChanged();
		}
		
		private function onAddedToStage(): void {
			addEventListener(MouseEvent.MOUSE_DOWN, onMousePressed);
			stage.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMoved);
			stage.addEventListener(MouseEvent.MOUSE_UP, onMouseReleased);
			addEventListener(MouseEvent.ROLL_OVER, onMouseOver);
			addEventListener(MouseEvent.ROLL_OUT, onMouseOut);
		}
		
		private function updateCursorPitch(): void {
			var scale: Array = Music.scaleFlags[doc.song.scale];
			
			var mousePitch: Number = Math.max(0, Math.min(pitchCount-1, pitchCount - (mouseY / pitchHeight)));
			if (scale[int(mousePitch) % 12] || doc.channel == 3) {
				cursorPitch = int(mousePitch);
			} else {
				var topPitch: int = int(mousePitch) + 1;
				var bottomPitch: int = int(mousePitch) - 1;
				while (scale[topPitch % 12] == false) {
					topPitch++;
				}
				while (scale[(bottomPitch) % 12] == false) {
					bottomPitch--;
				}
				var topRange: Number = topPitch;
				var bottomRange: Number = bottomPitch + 1;
				if (topPitch % 12 == 0 || topPitch % 12 == 7) {
					topRange -= 0.5;
				}
				if (bottomPitch % 12 == 0 || bottomPitch % 12 == 7) {
					bottomRange += 0.5;
				}
				cursorPitch = mousePitch - bottomRange > topRange - mousePitch ? topPitch : bottomPitch;
			}
		}
		
		private function onMouseOver(event: Event): void {
			mouseOver = true;
		}
		
		private function onMouseOut(event: Event): void {
			mouseOver = false;
		}
		
		private function onMousePressed(event: Event): void {
			mouseDown = true;
			doc.synth.pianoPressed = true;
			updatePreview();
		}
		
		private function onMouseMoved(event: Event): void {
			updateCursorPitch();
			doc.synth.pianoPitch = cursorPitch + doc.song.channelOctaves[doc.channel] * 12;
			updatePreview();
		}
		
		private function onMouseReleased(event: Event): void {
			mouseDown = false;
			doc.synth.pianoPressed = false;
			updatePreview();
		}
		
		private function updatePreview(): void {
			if (preview == null) return;
			
			preview.graphics.clear();
			if (!mouseOver || mouseDown) return;
			preview.graphics.lineStyle(2, 0xffffff);
			preview.graphics.drawRect(1, pitchHeight * (pitchCount - cursorPitch - 1) + 1, width - 2, pitchHeight - 2);
			preview.graphics.lineStyle();
		}
		
		private function documentChanged(): void {
			pitchHeight = doc.channel == 3 ? 43 : 13;
			pitchCount = doc.channel == 3 ? Music.drumCount : Music.pitchCount;
			updateCursorPitch();
			doc.synth.pianoPitch = cursorPitch + doc.song.channelOctaves[doc.channel] * 12;
			doc.synth.pianoChannel = doc.channel;
			render();
		}
		
		private function render(): void {
			if (preview == null) return;
			
			container.graphics.clear();
			container.graphics.beginFill(0x000000);
			container.graphics.drawRect(0, 0, width, height);
			container.graphics.endFill();
			
			var key: Bitmap;
			for each (key in keys) {
				container.removeChild(key);
			}
			keys = [];
			
			if (!doc.showLetters) return;
			
			if (doc.channel == 3) {
				for (var i: int = 0; i < Music.pitchCount; i++) {
					labels[i].text = "";
				}
			}
			for (var j: int = 0; j < pitchCount; j++) {
				var pitchNameIndex: int = (j + Music.keyTransposes[doc.song.key]) % 12;
				if (doc.channel == 3) {
					key = new Drum();
					key.scaleX = key.scaleY = 1.0 - ( j / pitchCount ) * 0.35;
					key.x = 16.0 - key.width / 2.0;
					key.y = 21.5 - key.height / 2.0;
					key.smoothing = true;
					var brightness: Number = 1.0 + ((j - pitchCount / 2.0) / pitchCount) * 0.5;
		            key.transform.colorTransform = new ColorTransform(brightness, brightness, brightness);
				} else if (Music.scaleFlags[doc.song.scale][j%12] == false) {
					labels[j].text = "";
					key = Music.pianoScaleFlags[pitchNameIndex] ? new WhiteKeyDisabled() : new BlackKeyDisabled();
				} else {
					labels[j].text = Music.pitchNames[pitchNameIndex];
					labels[j].textColor = Music.pianoScaleFlags[pitchNameIndex] ? 0x000000 : 0xffffff;
					key = Music.pianoScaleFlags[pitchNameIndex] ? new WhiteKey() : new BlackKey();
				}
				key.y += pitchHeight * (pitchCount - j - 1);
				container.addChildAt(key, 0);
				keys.push(key);
			}
			updatePreview();
		}
	]]></mx:Script>
	<mx:UIComponent id="container"/>
</mx:Canvas>
