<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (C) 2012 John Nesky

Permission is hereby granted, free of charge, to any person obtaining a copy of 
this software and associated documentation files (the "Software"), to deal in 
the Software without restriction, including without limitation the rights to 
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies 
of the Software, and to permit persons to whom the Software is furnished to do 
so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all 
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR 
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, 
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE 
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER 
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, 
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE 
SOFTWARE.
-->

<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
           xmlns:s="library://ns.adobe.com/flex/spark"
           xmlns="*"
           width="512"
           height="20"
           backgroundColor="0x000000"
           creationComplete="init()"
           addedToStage="onAddedToStage()">
	<mx:Script><![CDATA[
		import flash.display.*;
		import flash.events.*;
		import flash.geom.*;
		import flash.media.*;
		import flash.text.*;
		import flash.ui.*;
		import flash.utils.*;
		
		import mx.collections.ArrayCollection;
		
		import beepbox.synth.*;
		
		[Bindable]
		public var doc: Document;
		
		private var preview: Sprite;
		private var mouseDown: Boolean = false;
		private var mouseOver: Boolean = false;
		private var dragging: Boolean = false;
		private var dragStart: Number;
		
		private var barWidth: Number;
		
		public function init():void {
			preview = new Sprite();
			container.addChild(preview);
			doc.watch(documentChanged);
			documentChanged();
		}
		
		private function onAddedToStage(): void {
			addEventListener(MouseEvent.MOUSE_DOWN, onMousePressed);
			stage.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMoved);
			stage.addEventListener(MouseEvent.MOUSE_UP, onMouseReleased);
			addEventListener(MouseEvent.ROLL_OVER, onMouseOver);
			addEventListener(MouseEvent.ROLL_OUT, onMouseOut);
			documentChanged();
		}
		
		private function onMouseOver(event: Event): void {
			mouseOver = true;
		}
		
		private function onMouseOut(event: Event): void {
			mouseOver = false;
		}
		
		private function onMousePressed(event: Event): void {
			mouseDown = true;
			updatePreview();
			if (mouseX >= doc.barScrollPos * barWidth && mouseX <= (doc.barScrollPos + 16) * barWidth) {
				dragging = true;
				dragStart = mouseX;
			}
		}
		
		private function onMouseMoved(event: Event): void {
			if (dragging) {
				while (mouseX - dragStart < -barWidth * 0.5) {
					if (doc.barScrollPos > 0) {
						doc.barScrollPos--;
						dragStart -= barWidth;
						doc.changed();
					} else {
						break;
					}
				}
				while (mouseX - dragStart > barWidth * 0.5) {
					if (doc.barScrollPos < doc.song.bars - 16) {
						doc.barScrollPos++;
						dragStart += barWidth;
						doc.changed();
					} else {
						break;
					}
				}
			}
			updatePreview();
		}
		
		private function onMouseReleased(event: Event): void {
			if (!dragging && mouseDown) {
				if (mouseX < (doc.barScrollPos + 8) * barWidth) {
					if (doc.barScrollPos > 0) doc.barScrollPos--;
					doc.changed();
				} else {
					if (doc.barScrollPos < doc.song.bars - 16) doc.barScrollPos++;
					doc.changed();
				}
			}
			mouseDown = false;
			dragging = false;
			updatePreview();
		}
		
		private function updatePreview(): void {
			if (preview == null) return;
			
			preview.graphics.clear();
			if (!mouseOver || mouseDown) return;
			
			var center: Number = height * 0.5;
			var base: Number = 20;
			var tip: Number = 9;
			var arrowHeight: Number = 6;
			if (mouseX < doc.barScrollPos * barWidth) {
				preview.graphics.beginFill(0xffffff);
				preview.graphics.moveTo(tip, center);
				preview.graphics.lineTo(base, center + arrowHeight);
				preview.graphics.lineTo(base, center - arrowHeight);
				preview.graphics.lineTo(tip, center);
				preview.graphics.endFill();
			} else if (mouseX > (doc.barScrollPos + 16) * barWidth) {
				preview.graphics.beginFill(0xffffff);
				preview.graphics.moveTo(width - tip, center);
				preview.graphics.lineTo(width - base, center + arrowHeight);
				preview.graphics.lineTo(width - base, center - arrowHeight);
				preview.graphics.lineTo(width - tip, center);
				preview.graphics.endFill();
			} else {
				preview.graphics.lineStyle(2, 0xffffff);
				preview.graphics.drawRect(doc.barScrollPos * barWidth, 1, 16 * barWidth, height - 2);
				preview.graphics.lineStyle();
			}
		}
		
		private function documentChanged(): void {
			barWidth = (width-1) / Math.max(16, doc.song.bars);
			render();
		}
		
		private function render(): void {
			if (preview == null) return;
			if (stage == null) return;
			
			container.graphics.clear();
			container.graphics.beginFill(0x000000);
			container.graphics.drawRect(0, 0, width, height);
			container.graphics.endFill();
			
			container.graphics.beginFill(0x444444);
			container.graphics.drawRect(barWidth * doc.barScrollPos, 2, barWidth * 16, height - 4);
			container.graphics.endFill();
			
			for (var i: int = 0; i <= doc.song.bars; i++) {
				var lineWidth: int = (i % 16 == 0) ? 2 : 0;
				var lineHeight: int = (i % 16 == 0) ? 0 : ((i % 4 == 0) ? height / 8 : height / 3);
				container.graphics.lineStyle(lineWidth, 0x444444);
				container.graphics.moveTo(i * barWidth, lineHeight);
				container.graphics.lineTo(i * barWidth, height - lineHeight);
				container.graphics.lineStyle();
			}
			
			updatePreview();
		}
	]]></mx:Script>
	<mx:UIComponent id="container"/>
</mx:Canvas>
